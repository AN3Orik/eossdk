plugins {
	id 'java'
	id 'application'
	id 'distribution'
	id 'org.springframework.boot' version '3.1.1'
}

defaultTasks 'assemble'

distTar.enabled = false
bootJar.enabled = false
bootDistTar.enabled = false
bootDistZip.enabled = false
bootStartScripts.enabled = false

run.workingDir = new File("./")

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

application {
	mainClass = "host.anzo.eossdk.example.ExampleServer"
}

dependencies {
	compileOnly("org.jetbrains:annotations:26.0.0")

	annotationProcessor('org.projectlombok:lombok:1.18.34')
	compileOnly('org.projectlombok:lombok:1.18.34')

	implementation('org.slf4j:slf4j-api:2.0.16')
	implementation('org.slf4j:slf4j-ext:2.0.16')
	implementation('org.slf4j:slf4j-simple:2.0.16')
	implementation("org.apache.httpcomponents.client5:httpclient5-fluent:5.3.1")
	implementation("org.json:json:20240303")
	implementation(project(':eossdk'))

	def osName = System.getProperty("os.name").toLowerCase()
	if (osName.contains("windows")) {
		implementation(project(':eossdk-native-windows'))
	}
	else if (osName.contains("linux")) {
		implementation(project(':eossdk-native-linux'))
	}
	else if (osName.contains("mac") || osName.contains("darwin")) {
		implementation(project(':eossdk-native-mac'))
	}
}

jar {
	archiveClassifier = ''
	manifest {
		attributes(
				'Implementation-Title': project.name,
				'Built-Date': new Date().toString(),
				'Built-By': System.getProperty('user.name'),
				'Built-JDK': System.getProperty('java.version'),
				'Source-Compatibility': project.sourceCompatibility,
				'Target-Compatibility': project.targetCompatibility,
				'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' ')
		)
	}
}